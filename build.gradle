import com.opengamma.tools.gradle.sphinx.SphinxPlugin
import com.opengamma.tools.gradle.release.AutoVersionPlugin
import com.opengamma.tools.gradle.release.ReleasePlugin
import org.joda.beans.gradle.JodaBeansPlugin

ext {
	guavaVersion = "18.0"
	jodaConvertVersion = "1.7"
	jodaBeansVersion = "1.5.1"
	slf4jVersion = "1.7.5"
	ogAnalyticsVersion = "3.0.0-SNAPSHOT"

	testNGVersion = "6.8.21"
	assertJVersion = "2.0.0"
	mockitoVersion = "1.10.19"
	jacocoVersion = "0.7.4.201502262128"
}

allprojects {
    version "0.9.0-SNAPSHOT"
    group "com.opengamma.strata"
}

apply plugin: "base"
apply plugin: SphinxPlugin
apply plugin: AutoVersionPlugin
apply plugin: ReleasePlugin

sphinx {
    userGuideArchiveName = "strata-@project.version@-guide.zip"
}

subprojects {
    apply plugin: "java"
    apply from: "${rootProject.projectDir}/gradle/testConfig.gradle"
    apply plugin: "checkstyle"
    apply plugin: "jacoco"
    apply plugin: JodaBeansPlugin

    compileJava {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

	tasks.withType(JavaCompile) {
		options.compilerArgs << "-parameters"
	}

	dependencies {
		compile "com.google.guava:guava:${guavaVersion}"
		compile "org.joda:joda-convert:${jodaConvertVersion}"
		compile "org.joda:joda-beans:${jodaBeansVersion}"
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
	}

	task sourcesJar(type: Jar, dependsOn: jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives sourcesJar
	}

    checkstyle {
        String checkstyleConfiguration = this.class.getResource("/checkstyle/checkstyle.xml").text
        config = resources.text.fromString(checkstyleConfiguration)
        ignoreFailures = true
        showViolations = false
    }
    checkstyleMain.exclude('**/org/fpml/**')
    checkstyleMain.exclude('**/org/w3/**')

    jacoco {
        toolVersion = jacocoVersion
    }

    jodabeans {
        sourceDir = file('src/main/java').getAbsolutePath()
        indent = 2
        prefix = "_"
    }
}

task fullJavadoc(type: Javadoc) {
    group = "Documentation"
    description = "Produce combined Javadoc from all projects"

    failOnError = false
    verbose = false
    destinationDir = file("${buildDir}/docs/javadoc")
    options.windowTitle = "OpenGamma Strata API Documentation" // TODO

}

allprojects {
    afterEvaluate {
        rootProject.getTasksByName("javadoc", true).each { Task t ->
            if( ! t.enabled) return
            rootProject.tasks.fullJavadoc {
                source += files(t.project.sourceSets.collect { srcSet -> srcSet.allJava })
                classpath += files(t.project.sourceSets*.compileClasspath)
            }
        }
    }

	repositories {
		mavenLocal()
		maven {
			url "http://maven.opengamma.com/nexus/content/groups/public"
		}
		maven {
			url "http://maven.opengamma.com/nexus/content/groups/opengamma-developer"
			credentials {
				username ogNexusUsername
				password ogNexusPassword
			}
		}
	}
}

buildscript {
    repositories {
        mavenLocal()
        maven {
            url "http://maven.opengamma.com/nexus/content/groups/public"
        }
        maven {
            url "http://maven.opengamma.com/nexus/content/groups/opengamma-developer"
            credentials {
                username ogNexusUsername
                password ogNexusPassword
            }
        }
        maven {
            url "http://opengamma.artifactoryonline.com/opengamma/ext-releases-local"
            credentials {
                username ogNexusUsername
                password ogNexusPassword
            }
        }
    }
    dependencies {
        classpath "com.opengamma.tools:build-tools:1.2.2"
        classpath 'com.google.guava:guava:18.0'
	    classpath "com.opengamma.tools:gradle-simpleexec-plugin:${gradleSimpleExecPluginVersion}"
	    classpath "com.opengamma.tools:gradle-git-plugin:${gradleGitPluginVersion}"
	    classpath "com.opengamma.tools:gradle-sphinx-plugin:${gradleSphinxPluginVersion}"
	    classpath "com.opengamma.tools:gradle-og-release-plugin:${gradleOGReleasePluginVersion}"
        classpath group: 'org.joda', name: 'joda-beans', version: '1.4'
        classpath group: 'org.joda', name: 'joda-beans-plugin', version: '1.0.0'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.4"
}
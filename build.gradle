ext {
	guavaVersion = "18.0"
	jodaConvertVersion = "1.7"
	jodaBeansVersion = "1.5.1"
	slf4jVersion = "1.7.5"
	ogAnalyticsVersion = "3.0.0-SNAPSHOT"

	testNGVersion = "6.8.21"
	assertJVersion = "2.0.0"
	mockitoVersion = "1.10.19"
}

subprojects {
    apply plugin: "java"
    apply from: "${rootProject.projectDir}/gradle/testConfig.gradle"

    compileJava {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

	tasks.withType(JavaCompile) {
		options.compilerArgs << "-parameters"
	}

	dependencies {
		compile "com.google.guava:guava:${guavaVersion}"
		compile "org.joda:joda-convert:${jodaConvertVersion}"
		compile "org.joda:joda-beans:${jodaBeansVersion}"
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
	}

	task sourcesJar(type: Jar, dependsOn: jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives sourcesJar
	}
}

allprojects {
	repositories {
		mavenLocal()
		maven {
			url "http://maven.opengamma.com/nexus/content/groups/public"
		}
		maven {
			url "http://maven.opengamma.com/nexus/content/groups/opengamma-developer"
			credentials {
				username ogNexusUsername
				password ogNexusPassword
			}
		}
	}
}
